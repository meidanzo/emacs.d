;;; init-LaTeX-mode.el --- Emacs LaTeX ç¯å¢ƒæ­å»º -*- lexical-binding: t; -*-
;;; Commentary:
;;; é…ç½®æ’ä»¶: AUCTeX + RefTeX + CDLaTeX + amsreftex + auctex-cont-latexmk + auctex-label-numbers + evil-tex + ivy-bibtex
;;; + preview-auto


;; - ä¸»è¦åŠŸèƒ½
;; æ”¯æŒä¸­æ–‡ -> xetex
;; å¿«é€Ÿæ’å…¥ -> CDLaTeX
;; å¿«é€Ÿä¿®æ”¹ -> evil-tex
;; è‡ªåŠ¨é¡¹ç›®ç¼–è¯‘ -> LaTeXMk
;; åå‘æœç´¢ -> SyncTeX  + ~/.config/zathura/zathurarc
;; æ­£å‘æœç´¢(é«˜äº®å½“å‰è¡Œ) -> SyncTeX + zathura --synctex-forward + TeX-view
;; æŒç»­ç¼–è¯‘å’Œé¢„è§ˆ -> auctex-cont-latexmk + ~/.latexmkrc
;; æŠ˜å æ ‡è®° -> TeX-fold-mode
;; è‡ªåŠ¨é¢„è§ˆ: preview-auto(GUI) + prettify-symbols-mode(Terminal)
;; æ˜¾ç¤ºæŠ˜å çš„ç¼–å· -> auctex-label-numbers-mode
;; æœç´¢å’Œç®¡ç† BibTeX å‚è€ƒæ–‡çŒ® -> ivy-bibtex


;; - ä½¿ç”¨æ–¹æ³•
;; ç¼–è¯‘: C-c C-c -> LaTeXMk
;; å®æ—¶é¢„è§ˆ(pdf): C-c k -> auctex-cont-latexmk-toggle
;; è‡ªåŠ¨é¢„è§ˆ: GUI æ‰“å¼€æ–‡ä»¶çš„æ—¶å€™é€‰ y æˆ–è€… n, Terminal è‡ªåŠ¨å¼€å¯
;; æ­£å‘æœç´¢(PDF): C-c C-v -> Zathura-with-highlighting
;; åå‘æœç´¢ï¼šåœ¨ Zathura ä¸­æŒ‰ä¸­é”®æˆ– Ctrl+å·¦é”® (ä¾ Zathura é…ç½®)

;; - evilä¸­
;; leader(,) + "cm" / "lv": æ‰“å¼€å‘½ä»¤é€‰æ‹©åˆ—è¡¨
;; leader(,) + "ca": ç¼–è¯‘å¹¶æŸ¥çœ‹
;; leader(,) + "cn": æ‰“å¼€zathuraå¹¶é«˜äº®å…‰æ ‡æ‰€åœ¨è¡Œ
;; leader(,) + "ch": æ‰“å¼€zathuraå¹¶é«˜äº®å…‰æ ‡æ‰€åœ¨è¡Œ
;; leader(,) + "ce": æ’å…¥ç¯å¢ƒ
;; leader(,) + "ck": æŒç»­ç¼–è¯‘
;; leader(,) + "cd": æ‰“å¼€zathuraé¢„è§ˆ
;; leader(,) + "=" : æ˜¾ç¤ºç›®å½•
;; leader(,) + "lk": å…³é—­ç¼–è¯‘è¿›ç¨‹
;; leader(,) + "le": å±…ä¸­æ˜¾ç¤ºè¾“å‡ºç¼“å†²åŒº
;; leader(,) + "lt": æ˜¾ç¤º RefTeX çš„ Table of Contents
;; leader(,) + "lk": æ¸…ç†ä¸´æ—¶æ–‡ä»¶

;; æ³¨æ„:
;; æœ¬é…ç½®ä¸»è¦æ”¯æŒ LaTeX-mode, ä¸æ”¯æŒ plain-tex-mode doctex-mode, ç”±äºæœ¬é…ç½®ä½¿ç”¨çš„æ’ä»¶å¯¹å…¶æ”¯æŒä¸ä¸€.
;; ä¸€æ¬¡æ€§é…ç½®å®Œæˆæ¯”è¾ƒå¤æ‚, å¦‚æœ‰éœ€è¦è¿˜è¦çœ‹ç›¸å…³æ–‡æ¡£è°ƒæ•´ä»£ç .
;; åœ¨ GUI ä¸­é¢„è§ˆæ—¶, å¯èƒ½ä¼šå‘ç”Ÿå¡æ­».  Emacs å¡æ­», ç”¨ C-g ä¸­æ–­, å¤–éƒ¨è¿›ç¨‹å¡æ­», æ‰‹åŠ¨ TeX-kill-job.

;;; Code:

(defun my-auctex-setup ()
  "AUCTeX é…ç½®."
  (my-ensure 'tex)
  (setq-default TeX-master nil
                TeX-engine 'xetex)
  (setq TeX-auto-save t
        TeX-parse-self t
        TeX-auto-untabify t ;; ä¿å­˜æ—¶è‡ªåŠ¨å°† tab è½¬ä¸ºç©ºæ ¼
        TeX-command-default "LaTeXMk"
        TeX-PDF-mode t
        TeX-DVI-via-PDFTeX t
        TeX-file-line-error t  ;; è¾¾åˆ° file-line-error ä¸€æ ·çš„å‚æ•°æ•ˆæœ
        TeX-output-dir "build" ;; latexmkè‡ªåŠ¨ä½¿ç”¨ build/ è€Œä¸éœ€è¦é¢å¤–æŒ‡å®š -outdir
        TeX-save-query nil     ;; ç¼–è¯‘æ—¶è‡ªåŠ¨ä¿å­˜æ–‡ä»¶
        TeX-show-compilation nil ;; ç¼–è¯‘åœ¨åå°è¿›è¡Œ
        TeX-command-extra-options "-shell-escape -file-line-error -halt-on-error -interaction=nonstopmode -synctex=1")) ;; è®¾ç½®LaTeXMkç¼–è¯‘é€‰é¡¹

(defun my-synctex-setup ()
  "SyncTeX å…¨å±€é…ç½®.  å¯ä»¥é’ˆå¯¹æ‰€æœ‰*TeX-mode."
  ;; --- å¯åŠ¨ Emacs server (åå‘æœç´¢éœ€è¦) ---
  (require 'server)
  (unless (server-running-p)
    (server-start))

  (setq TeX-source-correlate-method 'synctex
        TeX-source-correlate-start-server t) ;; å…è®¸ PDF ç‚¹å‡»è·³è½¬, ç”¨äº SyncTeX åŠŸèƒ½: ä» PDF ç‚¹å‡»å¯ä»¥è·³è½¬åˆ° TeX æºç 
  (TeX-source-correlate-mode)                ;; æ‰“å¼€æºä»£ç å…³è”

  ;; --- PDF æŸ¥çœ‹å™¨è®¾ä¸º Zathura ---
  (setq TeX-view-program-list
        '(("Zathura" "zathura %o")
          ("Zathura-with-highlighting" "zathura --synctex-forward %n:0:%b %o")))

  (setq TeX-view-program-selection
        '((output-pdf "Zathura"))) ;; é»˜è®¤ C-c C-a ä¸ä½¿ç”¨åŒæ­¥

  (define-key TeX-mode-map (kbd "C-c C-v") #'my-TeX-view-with-SyncTeX-highlighting))

(defun my-TeX-view-with-SyncTeX-highlighting ()
  "Run zathura with SyncTeX highlighting."
  (interactive)
  (let ((TeX-view-program-selection
         '((output-pdf "Zathura-with-highlighting"))))
    (call-interactively 'TeX-view))) ;; æ­£ç¡®è°ƒç”¨æ–¹å¼

(defun my-reftex-setup ()
  "å¯ç”¨RefTeX(Emacså†…ç½®)."
  (my-ensure 'reftex)
  (when (locate-library "amsreftex")
    (my-ensure 'amsreftex))

  ;; å¯ç”¨ RefTeX, å¹¶é›†æˆåˆ° AucTeX ä¸­, åœ¨ LaTeX æ¨¡å¼ä¸­è‡ªåŠ¨å¯ç”¨ RefTeX
  (setq reftex-parse-all t
        reftex-enable-partial-scans t             ;; RefTeX åªæ‰«ææ­£åœ¨ç¼–è¾‘çš„éƒ¨åˆ†(æˆ–æœ€è¿‘ä¿®æ”¹çš„éƒ¨åˆ†),æé«˜æ€§èƒ½
        reftex-save-parse-info t
        reftex-use-multiple-selection-buffers t
        reftex-plug-into-AUCTeX t                 ;; è®© RefTeX ä¸ AUCTeX é…åˆæ›´ç´§å¯†, è¿™æ˜¯å…³é”®é…ç½®
        reftex-ref-macro-prompt nil               ;; ~ref<tab>~ åä¸æç¤ºç±»å‹
        reftex-trust-label-prefix t
        reftex-label-menu-flags '(t t nil nil t nil t t)
        reftex-toc-follow-mode t
        reftex-insert-label-flags '("stapd" "stapdf")
        reftex-ref-style-default-list '("Cleveref") ;; é»˜è®¤å¼•ç”¨é£æ ¼
        reftex-label-alist ; äº¤å‰å¼•ç”¨çš„è‡ªå®šä¹‰ç±»å‹
        '((nil ?e nil "\\cref{%s}" nil nil) ; ä¸ cref é…åˆä½¿ç”¨.
          ("theorem" ?t "thm:" nil t ("Theorem" "å®šç†"))
          ("proposition" ?p "prop:" nil t ("Proposition" "å‘½é¢˜"))
          ("definition" ?d "def:" nil t ("Definition" ))
          ("lemma" ?a "lem:" nil t ("Lemma" "å¼•ç†")))))

(defun my-preview-setup ()
  "é¢„è§ˆä¸ç¾åŒ–."
  ;; å…‰æ ‡ç§»åŠ¨åˆ°ä¸€ä¸ªè¢«æ›¿æ¢æˆç¬¦å·çš„åœ°æ–¹æ—¶, æš‚æ—¶æ˜¾ç¤ºåŸå§‹æ–‡æœ¬
  (setq prettify-symbols-unprettify-at-point 'right-edge)
  ;; set-fontset-font å…¨å±€å‰¯ä½œç”¨, ä½†æˆ‘ä¸åœ¨ä¹.
  ;; (set-fontset-font "fontset-default" '(#x10000 . #x1FFFF) "Cambria Math" nil 'prepend)
  (set-fontset-font "fontset-default" 'mathematical "Cambria Math")
  (prettify-symbols-mode)) ;; é¢„è§ˆtexæ–‡ä»¶, æŠŠæŸäº›å­—ç¬¦è‡ªåŠ¨ç”¨ Unicode ç¬¦å·æ˜¾ç¤º.
                           ;; é¡»åœ¨(my-more-prettified-symbols) ä¹‹å. å¦åˆ™å¯èƒ½ä¸ç”Ÿæ•ˆ.

(defun my-label-numbers ()
  (when (derived-mode-p 'LaTeX-mode)
    (my-ensure 'auctex-label-numbers)
    ;; æ˜¾ç¤ºæŠ˜å çš„æ ‡ç­¾çš„ç¼–å·.å¦‚æœä¿®æ”¹éœ€è¦é‡æ–°ç¼–è¯‘*TeXæ–‡ä»¶
    ;; éœ€è¦å¯ç”¨TeX-fold-mode
    ;; auctex-label-numbers-modeéœ€ç¼–è¯‘ä¹‹åè¯»å–.aux æ–‡ä»¶æ‰èƒ½ç”Ÿæ•ˆ
    ;; æ›´æ–°è¿™äº›ç¼–å·, éœ€è¦é‡æ–°ç¼–è¯‘æ–‡æ¡£, é‡æ–°ç”Ÿæˆé¢„è§ˆ, kå¹¶åˆ·æ–°æŠ˜å 
    (auctex-label-numbers-mode 1)
    ;; ç¡®ä¿ font-lock å®ŒæˆåæŠ˜å 
    (font-lock-ensure)
    ;; run TeX-fold-buffer *after* font-lock is ready
    ;; TeX-fold-type-listæä¾›æŠ˜å å†…å®¹
    (TeX-fold-buffer))) ;; è‡ªåŠ¨æŠ˜å æ‰€æœ‰å¯æŠ˜å å†…å®¹(æ³¨é‡Š,æ ‡ç­¾ç­‰)

(defun my-refresh-auctex-label-numbers (&rest _)
  "æ‰‹åŠ¨ç¼–è¯‘æ—¶æ›´æ–°æ ‡è®°å’Œå·ç , åœ¨TeX-command-buffer ä¸­è¿è¡Œ, ä¿ç•™å…‰æ ‡ä½ç½®."
  (declare (ignore _))
  (when (and (boundp 'TeX-command-buffer)
             TeX-command-buffer
             (bound-and-true-p auctex-label-numbers-mode)
             (buffer-live-p TeX-command-buffer))
    (with-current-buffer TeX-command-buffer       ;; è·å– LaTeX æºæ–‡ä»¶ buffer
      (condition-case err
          (let ((current-point (point)))
            ;; æ³¨æ„: é¡ºåºä¸èƒ½é”™!
            (auctex-label-numbers-mode -1)        ;; æ¸…é™¤æ—§çš„æ ‡ç­¾å·ç¼“å­˜
            (redisplay)                           ;; éé˜»å¡åˆ·æ–°, æˆ–è€… (force-window-update nil)
            (auctex-label-numbers-mode 1)         ;; ä»æ›´æ–°çš„ .aux æ–‡ä»¶é‡æ–°è¯»å–æ ‡ç­¾å·
            (when (and (fboundp 'TeX-fold-buffer)
                       (bound-and-true-p TeX-fold-mode))
              (TeX-fold-buffer))                  ;; ä½¿ç”¨æ–°ç¼“å­˜è¿›è¡ŒæŠ˜å æ˜¾ç¤º
            (goto-char current-point))            ;; æ¢å¤å…‰æ ‡ä½ç½®
        (error
         (message "åˆ·æ–° auctex label numbers æ—¶å‡ºé”™: %s" err))))))

(defun my-refresh-auctex-label-numbers-in-flymake ()
  "æŒç»­ç¼–è¯‘æ—¶, åœ¨ Flymake è¯Šæ–­æŠ¥å‘Šååˆ·æ–°æ ‡ç­¾å·."
  (when (and (derived-mode-p 'LaTeX-mode)
             (bound-and-true-p auctex-label-numbers-mode)
             (not (buffer-modified-p)))
    (condition-case err
        (let ((current-point (point)))
          (auctex-label-numbers-mode -1)
          (sit-for 0.1)
          (redisplay)
          (auctex-label-numbers-mode 1)
          (sit-for 0.2)
          (when (and (fboundp 'TeX-fold-buffer)
                     (bound-and-true-p TeX-fold-mode))
            (TeX-fold-buffer))
          (goto-char current-point))
      (error
       (message "åˆ·æ–° auctex label numbers æ—¶å‡ºé”™:  %s" err)))))

(defun my-auctex-cont-latexmk-setup ()
  "æŒç»­ç¼–è¯‘."
  (my-ensure 'auctex-cont-latexmk)
  (setq auctex-cont-latexmk-command
        '("latexmk -xelatex -pvc -e "
          ("$xelatex=q/xelatex %O -shell-escape -file-line-error -halt-on-error -interaction=nonstopmode -synctex=1   %S/")))

  ;; é˜²æ­¢ reload ä¹‹å, advice å åŠ 
  (advice-remove 'auctex-cont-latexmk-send-report
                 #'my-refresh-auctex-label-numbers-in-flymake)

  ;; åœ¨ Flymake æŠ¥å‘Šååˆ·æ–°æ ‡ç­¾å·
  (advice-add 'auctex-cont-latexmk-send-report
              :after
              (lambda ()
                (when auctex-cont-latexmk-mode
                  (run-with-timer 0.5 nil
                                 #'my-refresh-auctex-label-numbers-in-flymake))))

  ;; é¦–æ¬¡å¯ç”¨æŒç»­ç¼–è¯‘æ—¶åˆ·æ–°ä¸€æ¬¡æ ‡ç­¾å·, ä¸ºå¤§æ–‡ä»¶å¤§é¡¹ç›®è®¾è®¡
  (advice-add 'auctex-cont-latexmk-turn-on
              :after
              (lambda ()
                  (run-with-timer 1.0 nil
                                  #'my-refresh-auctex-label-numbers-in-flymake)))

  (define-key TeX-mode-map (kbd "C-c k") #'auctex-cont-latexmk-toggle))


;; {{
;; ä»¥ä¸‹å‡½æ•°æ”¹ç¼–è‡ª https://gitee.com/mickey991/emacs-config.git
;; Original source:
;;   https://gitee.com/mickey991/emacs-config.git
;; Copyright (c) 2023 é‡‘è‰²é£è´¼ (å°ç±³)
;; Licensed under the MIT License
;; - my-TeX-fold-config
;; - my-LaTeX-fonts-config
;; - my-more-prettified-symbols
;; - my-set-cdlatex-command-alist
;; - my-set-cdlatex-env-alist
;; - my-set-cdlatex-math-modify-alist
;; - my-set-cdlatex-math-symbol-alist
;; - my-evil-tex-incre-delim-size
;; - my-evil-tex-decre-delim-size
;; - my-evil-tex-toggle-delim-type
;; - my-preview-latex-config

(defun my-TeX-fold-config ()
  "ç¯å¢ƒæŠ˜å ."
  ;; (setq TeX-fold-type-list '(env macro comment math)
  (setq TeX-fold-type-list '(env macro comment)
        TeX-fold-env-spec-list '(("[comment]" ("comment")) ("[proof]" ("proof")))
        LaTeX-fold-env-spec-list '(("frame" ("frame"))))
  (dolist (spec
           '(("[c]" ("cite"))
             ("[l]" ("label"))
             ("[r]" ("ref" "pageref" "eqref" "footref"))))
    (add-to-list 'TeX-fold-macro-spec-list spec)))

(defun my-LaTeX-fonts-config ()
  "æ•°å­¦å­—ä½“."
  (setq LaTeX-font-list
        (append
         '((?m "\\textmc{" "}" "\\mathmc{" "}")
           (?g "\\textgt{" "}" "\\mathgt{" "}")
           (?e "\\en{" "}")
           (?c "\\cn{" "}")
           (?4 "$" "$"))
         LaTeX-font-list)))

;; @see https://en.wikipedia.org/wiki/Mathematical_operators_and_symbols_in_Unicode
(defun my-more-prettified-symbols ()
  (my-ensure 'tex-mode) ; è½½å…¥ tex--prettify-symbols-alist å˜é‡
  (mapc (lambda (pair) (delete pair tex--prettify-symbols-alist))
        '(("\\supset" . 8835)))
  (mapc (lambda (pair) (cl-pushnew pair tex--prettify-symbols-alist))
        '(;; brackets
          ("\\big(" . ?\N{Z notation left image bracket}) ; â¦‡, #x2987
          ("\\bigl(" . ?\N{Z notation left image bracket}) ; â¦‡, #x2987
          ("\\big)" . ?\N{Z notation right image bracket}) ; â¦ˆ #x2988
          ("\\bigr)" . ?\N{Z notation right image bracket}) ; â¦ˆ #x2988
          ("\\Big(" . ?\N{left white parenthesis}); â¦… #x2985
          ("\\Bigl(" . ?\N{left white parenthesis}); â¦… #x2985
          ("\\Big)" . ?\N{right white parenthesis}) ; â¦† #x2986
          ("\\Bigr)" . ?\N{right white parenthesis}) ; â¦† #x2986
          ("\\bigg(" . ?\N{left double parenthesis}) ; â¸¨
          ("\\biggl(" . ?\N{left double parenthesis}) ; â¸¨
          ("\\bigg)" . ?\N{right double parenthesis}) ; â¸©
          ("\\biggr)" . ?\N{right double parenthesis}) ; â¸©
          ("\\big[" . ?\N{mathematical left white tortoise shell bracket}) ; âŸ¬
          ("\\bigl[" . ?\N{mathematical left white tortoise shell bracket}) ; âŸ¬
          ("\\big]" . ?\N{mathematical right white tortoise shell bracket}) ; âŸ­
          ("\\bigr]" . ?\N{mathematical right white tortoise shell bracket}) ; âŸ­
          ("\\Big[" . ?\N{mathematical left white square bracket}) ; âŸ¦ #x27E6
          ("\\Bigl[" . ?\N{mathematical left white square bracket}) ; âŸ¦ #x27E6
          ("\\Big]" . ?\N{mathematical right white square bracket}) ; âŸ§ #x27E7
          ("\\Bigr]" . ?\N{mathematical right white square bracket}) ; âŸ§ #x27E7
          ("\\bigg[" . ?\N{left white lenticular bracket}) ; ã€–
          ("\\biggl[" . ?\N{left white lenticular bracket}) ; ã€–
          ("\\bigg]" . ?\N{right white lenticular bracket}) ; ã€—
          ("\\biggr]" . ?\N{right white lenticular bracket}) ; ã€—
          ("\\{" . ?\N{medium left curly bracket ornament}) ; â´
          ("\\}" . ?\N{medium right curly bracket ornament}) ; âµ
          ("\\big\\{" . ?\N{left white curly bracket}) ; â¦ƒ
          ("\\bigl\\{" . ?\N{left white curly bracket}) ; â¦ƒ
          ("\\big\\}" . ?\N{right white curly bracket}) ; â¦„
          ("\\bigr\\}" . ?\N{right white curly bracket}) ; â¦„
          ("\\Big\\{" . ?\N{left arc less-than bracket}) ; â¦“
          ("\\Bigl\\{" . ?\N{left arc less-than bracket}) ; â¦“
          ("\\Big\\}" . ?\N{right arc greater-than bracket}) ; â¦”
          ("\\Bigr\\}" . ?\N{right arc greater-than bracket}) ; â¦”
          ("\\bigg\\{" . ?\N{double left arc greater-than bracket}) ; â¦•
          ("\\biggl\\{" . ?\N{double left arc greater-than bracket}) ; â¦•
          ("\\bigg\\}" . ?\N{double right arc less-than bracket}) ; â¦–
          ("\\biggr\\}" . ?\N{double right arc less-than bracket}) ; â¦–
          ("\\big|" .?\N{left wiggly fence}) ; â§˜
          ("\\bigl|" .?\N{left wiggly fence}) ; â§˜
          ("\\bigr|" .?\N{left wiggly fence}) ; â§˜
          ("\\lvert" .?\N{left wiggly fence}) ; â§˜
          ("\\rvert" .?\N{left wiggly fence}) ; â§š
          ("\\Big|" .?\N{left double wiggly fence}) ; â§š
          ("\\Bigl|" .?\N{left double wiggly fence}) ; â§š
          ("\\Bigr|" .?\N{left double wiggly fence}) ; â§š
          ("\\lVert" .?\N{DOUBLE VERTICAL LINE}) ; â€–
          ("\\rVert" .?\N{DOUBLE VERTICAL LINE}) ; â€–
          ("\\coloneq" .?\N{colon equal}); â‰”
          ("\\eqcolon" .?\N{equal colon}); â‰•
          ;; blackboard bold/double-struck
          ("\\Z" . ?\N{double-struck capital Z}) ; â„¤ 8484
          ("\\Q" . ?\N{double-struck capital Q}) ; â„š 8474
          ("\\N" . ?\N{double-struck capital N}) ; â„• 8469
          ("\\R" . ?\N{double-struck capital R}) ; â„ 8477
          ("\\PP" . ?\N{double-struck capital P}) ; â„™ #x2119
          ("\\HH" . ?\N{double-struck capital H}) ; â„
          ("\\EE" . ?\N{mathematical double-struck capital E}) ; ğ”¼ #x1D53C
          ("\\mathbb{S}" . ?\N{mathematical double-struck capital S}) ; ğ•Š #x1D54A
          ("\\ONE" . ?\N{mathematical double-struck digit ONE}) ; ğŸ™ #x1D7D9
          ;; bold face
          ("\\Pp" . ?\N{mathematical bold capital P}) ; ğ #x1D40F
          ("\\Qq" . ?\N{mathematical bold capital Q}) ; ğ
          ("\\Ee" . ?\N{mathematical bold capital E}) ; ğ„ #x1D404
          ("\\bb" . ?\N{mathematical bold small b}) ; ğ›
          ("\\mm" . ?\N{mathematical bold small m}) ; ğ¦
          ;; calligraphy
          ("\\Fc" . ?\N{script capital F}) ; â„± #x2131
          ("\\Nc" . ?\N{mathematical script capital N}) ; ğ’© #x1D4A9
          ("\\Zc" . ?\N{mathematical script capital Z}) ; ğ’µ
          ("\\Pc" . ?\N{mathematical script capital P}) ; ğ’«
          ("\\Qc" . ?\N{mathematical script capital Q}) ; ğ’«
          ;; san-serif
          ("\\P" . ?\N{mathematical sans-serif capital P}) ; ğ–¯ #x1D5AF
          ("\\E" . ?\N{mathematical sans-serif capital E}) ; ğ–¤ #x1D5A4
          ;; others
          ("\\supset" . ?\N{superset of}) ; âŠƒ
          ("\\temp" . ?\N{mathematical italic kappa symbol}) ; ğœ˜, varkappa
          ("\\varnothing" . ?\N{empty set}) ; âˆ…
          ("\\dotsb" . 8943)
          ("\\dotsc" . 8230)
          ("\\eps" . 949))))


(defun my-set-cdlatex-command-alist ()
  "ä¸ºäº†é¿å…å’Œ yasnippet å‘ç”Ÿå†²çª, åªèƒ½ä¿®æ”¹é”®."
  (setq cdlatex-command-alist
        '(("lr|" "insert \\lvert ? \\rvert" "\\lvert ? \\rvert" cdlatex-position-cursor nil nil t)
          ("lv" "insert \\lVert ? \\rVert" "\\lVert ? \\rVert" cdlatex-position-cursor nil nil t)
          ("sb" "insert pairs of \\[ \\]" "\\[ ? \\]" cdlatex-position-cursor nil t t)
          ("ce" "insert :=" "\\coloneq " nil nil nil t)
          ("ec" "insert =:" "\\eqcolon " nil nil nil t)
          ("Big(" "insert \\Bigl( \\Bigr)" "\\Bigl( ? \\Bigr" cdlatex-position-cursor nil nil t)
          ("Big[" "insert \\Bigl[ \\Bigr]" "\\Bigl[ ? \\Bigr" cdlatex-position-cursor nil nil t)
          ("Big\\|" "insert \\Big\\lVert \\Big\\rVert" "\\Big\\lVert ? \\Big\\rVert" cdlatex-position-cursor nil nil t)
          ("Big{" "insert \\Bigl\\{ \\Bigr\\}" "\\Bigl\\{ ? \\Bigr\\" cdlatex-position-cursor nil nil t)
          ("Big|" "insert \\Big\\lvert \\Bigr\rvert" "\\Big\\lvert ? \\Big\\rvert" cdlatex-position-cursor nil nil t)
          ("aali" "insert equation" "\\left\\{\n\\begin{aligned}\n? \n\\end{aligned}\\right." cdlatex-position-cursor nil nil t)
          ("alb" "Insert beamer alert block with overlay" "\\begin{alertblock}<+->{ ? } \n\n\\end{alertblock}" cdlatex-position-cursor nil t nil)
          ("alb*" "Insert beamer alert block without overlay" "\\begin{alertblock}{ ? } \n\n\\end{alertblock}" cdlatex-position-cursor nil t nil)
          ("big(" "insert \\bigl( \\bigr)" "\\bigl( ? \\bigr" cdlatex-position-cursor nil nil t)
          ("big[" "insert \\bigl[ \\bigr]" "\\bigl[ ? \\bigr" cdlatex-position-cursor nil nil t)
          ("big\\|" "insert \\big\\lvert \\big\\rvert" "\\big\\lvert ? \\big\\rvert" cdlatex-position-cursor nil nil t)
          ("big{" "insert \\bigl\\{ \\bigr\\}" "\\bigl\\{ ? \\bigr\\" cdlatex-position-cursor nil nil t)
          ("big|" "insert \\big\\lvert \\big\\rvert" "\\big\\lvert ? \\big\\rvert" cdlatex-position-cursor nil nil t)
          ("bigg(" "insert \\biggl( \\biggr)" "\\biggl( ? \\biggr" cdlatex-position-cursor nil nil t)
          ("bigg[" "insert \\biggl[ \\biggr]" "\\biggl[ ? \\biggr" cdlatex-position-cursor nil nil t)
          ("bigg\\|" "insert \\bigg\\lvert \\bigg\\rvert" "\\bigg\\lvert ? \\bigg\\rvert" cdlatex-position-cursor nil nil t)
          ("bigg{" "insert \\biggl\\{ \\biggr\\}" "\\biggl\\{ ? \\biggr\\" cdlatex-position-cursor nil nil t)
          ("bigg|" "insert \\bigg\\lvert \\bigg\\rvert" "\\bigg\\lvert ? \\bigg\\rvert" cdlatex-position-cursor nil nil t)
          ("Bigg(" "insert \\Biggl( \\Biggr)" "\\Biggl( ? \\Biggr" cdlatex-position-cursor nil nil t)
          ("Bigg[" "insert \\Biggl[ \\Biggr]" "\\Biggl[ ? \\Biggr" cdlatex-position-cursor nil nil t)
          ("Bigg\\|" "insert \\Bigg\\lvert \\Bigg\\rvert" "\\Bigg\\lvert ? \\Bigg\\rvert" cdlatex-position-cursor nil nil t)
          ("Bigg{" "insert \\Biggl\\{ \\Biggr\\}" "\\Biggl\\{ ? \\Biggr\\" cdlatex-position-cursor nil nil t)
          ("Bigg|" "insert \\Bigg\\lvert \\Bigg\\rvert" "\\Bigg\\lvert ? \\Bigg\\rvert" cdlatex-position-cursor nil nil t)
          ("fa" "Insert \\frac{}{}" "\\frac{?}{}" cdlatex-position-cursor nil nil t)           ; ä¸èƒ½æ˜¯ fr
          ("fc" "Insert \\frac{}{}" "\\frac{?}{}" cdlatex-position-cursor nil nil t)
          ("an" "Insert an ALIGN environment template" "" cdlatex-environment ("align") t nil) ; ä¸èƒ½æ˜¯ ali
          ("ag" "Insert an ALIGN environment template" "" cdlatex-environment ("align") t nil) ; ä¸èƒ½æ˜¯ ali
          ("align" "Insert an ALIGN environment template" "" cdlatex-environment ("align") t nil)
          ("ie" "New item in current environment" "" cdlatex-item nil t t)   ; ä¸èƒ½æ˜¯ it
          ("blo" "Insert beamer block with overlay" "\\begin{block}<+->{ ? } \n\n\\end{block}" cdlatex-position-cursor nil t nil)
          ("blo*" "Insert beamer block WITHOUT overlay" "\\begin{block}{ ? } \n\n\\end{block}" cdlatex-position-cursor nil t nil)
          ("bn" "binomial" "\\binom{?}{}" cdlatex-position-cursor nil nil t)
          ("capl" "insert \\bigcap\\limits_{}^{}" "\\bigcap\\limits_{?}^{}" cdlatex-position-cursor nil nil t)
          ("case" "insert cases" "\\begin{cases}\n? & \\\\\n &\n\\end{cases}" cdlatex-position-cursor nil nil t)
          ("cd" "insert dotsb" "\\dotsb" nil nil t t)
          ("cupl" "insert \\bigcup\\limits_{}^{}" "\\bigcup\\limits_{?}^{}" cdlatex-position-cursor nil nil t)
          ("ds" "insert ddots" "\\ddots" nil nil t t)
          ("def" "insert definition env" "" cdlatex-environment ("definition") t nil)
          ("des" "insert description" "" cdlatex-environment ("description") t nil)
          ("enu*" "insert enu" "\\begin{enumerate}\n\\item ?\n\\end{enumerate}" cdlatex-position-cursor nil t nil)
          ("equ*" "insert unlabel equation" "" cdlatex-environment ("equation*") t nil)
          ("exb" "Insert beamer example block with overlay" "\\begin{exampleblock}<+->{ ? } \n\n\\end{exampleblock}" cdlatex-position-cursor nil t nil)
          ("exb*" "Insert beamer example block without overlay" "\\begin{exampleblock}{ ? } \n\n\\end{exampleblock}" cdlatex-position-cursor nil t nil)
          ("exe" "Insert exercise" "\\begin{exercise}\n? \n\\end{exercise}" cdlatex-position-cursor nil t nil)
          ("fra" "insert frame (for beamer)" "" cdlatex-environment ("frame") t nil)
          ("hhl" "insert \\ \\hline" "\\\\ \\hline" ignore nil t nil)
          ("hl" "insert \\hline" "\\hline" ignore nil t nil)
          ("ipenu" "insert in paragraph enumerate" "" cdlatex-environment ("inparaenum") t nil)
          ("ipite" "insert in paragraph itemize" "" cdlatex-environment ("inparaitem") t nil)
          ("im" "insert \\item" "\\item?" cdlatex-position-cursor nil t nil)
          ("ld" "insert dotsc" "\\dotsc" nil nil t t)
          ("lma" "insert lemma env" "" cdlatex-environment ("lemma") t nil)
          ("liml" "insert \\lim\\limits_{}" "\\lim\\limits_{?}" cdlatex-position-cursor nil nil t)
          ("lr<" "insert bra-ket" "\\langle ? \\rangle" cdlatex-position-cursor nil nil t)
          ("myenu" "insert in my enumerate for beamer" "" cdlatex-environment ("myenumerate") t nil)
          ("myite" "insert in my itemize for beamer" "" cdlatex-environment ("myitemize") t nil)
          ("ons" "" "\\onslide<?>{ }" cdlatex-position-cursor nil t t)
          ("pa" "insert pause" "\\pause" ignore nil t nil)
          ("pro" "insert proof env" "" cdlatex-environment ("proof") t nil)
          ("prodl" "insert \\prod\\limits_{}^{}" " \\prod\\limits_{?}^{}" cdlatex-position-cursor nil nil t)
          ("prop" "insert proposition" "" cdlatex-environment ("proposition") t nil)
          ("se" "insert \\{\\}" "\\{ ? \\}" cdlatex-position-cursor nil nil t)
          ("spl" "insert split" "" cdlatex-environment ("split") nil t)
          ("st" "stackrel" "\\stackrel{?}{}" cdlatex-position-cursor nil nil t)
          ("te" "insert text" "\\text{?}" cdlatex-position-cursor nil nil t)
          ("the" "insert theorem env" "" cdlatex-environment ("theorem") t nil)
          ("cte" "insert citation using ivy-bibtex" "" ivy-bibtex-with-local-bibliography nil t nil)
          ("vd" "insert vdots" "\\vdots" nil nil t t))))

(defun my-set-cdlatex-env-alist ()
  (setq cdlatex-env-alist
        '(("definition" "\\begin{definition}\n\\label{def:?}\n\n\\end{definition}" nil)
          ("enumerate" "\\begin{enumerate}[?]\n\\item \n\\end{enumerate}" "\\item ?")
          ("equation*" "\\begin{equation*}\n? \n\\end{equation*}" nil)
          ("exercise" "\\begin{exercise}[?]\n\n\\end{exercise}" nil)
          ("frame" "\\begin{frame}{ ? }\n\n\\end{frame}" nil)
          ("inparaenum" "\\begin{inparaenum}\n\\item ? \n\\end{inparaenum}" "\\item ?")
          ("inparaitem" "\\begin{inparaitem}\n\\item ?\n\\end{inparaitem}" "\\item ?")
          ("lemma" "\\begin{lemma}\n\\label{lem:?}\n\n\\end{lemma}" nil)
          ("myenumerate" "\\begin{myenumerate}\n\\item ?\n\\end{myenumerate}" "\\item ?")
          ("myitemize" "\\begin{myitemize}\n\\item ?\n\\end{myitemize}" "\\item ?")
          ("proof" "\\begin{proof}?\n\n\\end{proof}" nil)
          ("proposition" "\\begin{proposition}\n\n\\end{proposition}" nil)
          ("theorem" "\\begin{theorem}\n\\label{thm:?}\n\n\\end{theorem}" nil))))

(defun my-set-cdlatex-math-modify-alist ()
  (setq cdlatex-math-modify-alist
        '((?k "\\mathfrak" "" t nil nil)
          (?t "\\mathbb" "" t nil nil))))

(defun my-set-cdlatex-math-symbol-alist ()
  (setq cdlatex-math-symbol-alist
        '((?0 ("\\varnothing" "\\emptyset"))
          (?1 ("\\ONE" "\\one"))
          (?. ("\\cdot" "\\circ"))
          (?v ("\\vee" "\\bigvee"))
          (?& ("\\wedge" "\\bigwedge"))
          (?9 ("\\cap" "\\bigcap" "\\bigoplus"))
          (?+ ("\\cup" "\\bigcup" "\\oplus"))
          (?- ("\\rightharpoonup" "\\hookrightarrow" "\\circlearrowleft"))
          (?= ("\\equiv" "\\Leftrightarrow" "\\Longleftrightarrow"))
          (?~ ("\\sim" "\\approx" "\\propto"))
          (?L ("\\Lambda" "\\limits"))
          (?* ("\\times" "\\otimes" "\\bigotimes"))
          (?e ("\\eps" "\\epsilon" "\\exp\\Big( ? \\Big)"))
          (?> ("\\mapsto" "\\longrightarrow" "\\rightrightarrows"))
          (?< ("\\preceq" "\\leftarrow" "\\longleftarrow"))
          (?| ("\\parallel" "\\mid" "\\perp"))
          (?S ("\\Sigma" "\\sum_{?}^{}"))
          (?{ ("\\subset" "\\prec" "\\subseteq"))
          (?} ("\\supset" "\\succ" "\\supseteq")))))

(defun my-evil-tex-incre-delim-size ()
  "Cycle through delimiter sizes from small to large: normal size ->\big->\Big->\bigg->\Bigg."
  (interactive)
  (let* ((case-fold-search nil)
	   (outer (evil-tex-a-delim)) (inner (evil-tex-inner-delim))
	   (left-over (ignore-errors (make-overlay (car outer) (car inner))))
	   (right-over (ignore-errors (make-overlay (cadr inner) (cadr outer)))))
    (when (and left-over right-over outer inner)
	(save-excursion
	  (let ((left-str (buffer-substring-no-properties (overlay-start left-over) (overlay-end left-over)))
		(right-str (buffer-substring-no-properties (overlay-start right-over) (overlay-end right-over))))
	    (goto-char (overlay-start left-over))
	    (cl-destructuring-bind (l . r)
		(cond ; note: bigg/Bigg must be before big/Big
		 ((looking-at "\\\\\\(?:Bigg\\)") ; cycle to no modifiers
		  (cons (replace-regexp-in-string
			 "\\\\\\(?:Biggl\\|Bigg\\)" "" left-str)
			(replace-regexp-in-string
			 "\\\\\\(?:Biggr\\|Bigg\\)" "" right-str)))
		 ((looking-at "\\\\\\(?:bigg\\)")
		  (cons (replace-regexp-in-string
			 "\\\\bigg" "\\\\Bigg" left-str)
			(replace-regexp-in-string
			 "\\\\bigg" "\\\\Bigg" right-str)))
		 ((looking-at "\\\\\\(?:Big\\)")
		  (cons (replace-regexp-in-string
			 "\\\\Big" "\\\\bigg" left-str)
			(replace-regexp-in-string
			 "\\\\Big" "\\\\bigg" right-str)))
		 ((looking-at "\\\\\\(?:big\\)")
		  (cons (replace-regexp-in-string
			 "\\\\big" "\\\\Big" left-str)
			(replace-regexp-in-string
			 "\\\\big" "\\\\Big" right-str)))
		 (t (cons (concat "\\bigl" left-str)
			  (concat "\\bigr" right-str))))
	      (evil-tex--overlay-replace left-over  l)
	      (evil-tex--overlay-replace right-over r)))
	  (delete-overlay left-over) (delete-overlay right-over)))))

(defun my-evil-tex-decre-delim-size ()
  "Cycle through delimiter sizes from large to small : \Bigg->\bigg->\Big->\big-> normal size."
  (interactive)
  (let* ((case-fold-search nil)
	   (outer (evil-tex-a-delim)) (inner (evil-tex-inner-delim))
	   (left-over (ignore-errors (make-overlay (car outer) (car inner))))
	   (right-over (ignore-errors (make-overlay (cadr inner) (cadr outer)))))
    (when (and left-over right-over outer inner)
	(save-excursion
	  (let ((left-str (buffer-substring-no-properties (overlay-start left-over) (overlay-end left-over)))
		(right-str (buffer-substring-no-properties (overlay-start right-over) (overlay-end right-over))))
	    (goto-char (overlay-start left-over))
	    (cl-destructuring-bind (l . r)
		(cond ; note: bigg/Bigg must be before big/Big
		 ((looking-at "\\\\\\(?:Bigg\\)")
		  (cons (replace-regexp-in-string
			 "\\\\Bigg" "\\\\bigg" left-str)
			(replace-regexp-in-string
			 "\\\\Bigg" "\\\\bigg" right-str)))
		 ((looking-at "\\\\\\(?:bigg\\)")
		  (cons (replace-regexp-in-string
			 "\\\\bigg" "\\\\Big" left-str)
			(replace-regexp-in-string
			 "\\\\bigg" "\\\\Big" right-str)))
		 ((looking-at "\\\\\\(?:Big\\)")
		  (cons (replace-regexp-in-string
			 "\\\\Big" "\\\\big" left-str)
			(replace-regexp-in-string
			 "\\\\Big" "\\\\big" right-str)))
		 ((looking-at "\\\\\\(?:big\\)")
		  (cons (replace-regexp-in-string
			 "\\\\\\(?:bigl\\|big\\)" "" left-str)
			(replace-regexp-in-string
			 "\\\\\\(?:bigr\\|big\\)" "" right-str)))
		 (t
		  (cons (concat "\\Biggl" left-str)
			(concat "\\Biggr" right-str))))
	      (evil-tex--overlay-replace left-over  l)
	      (evil-tex--overlay-replace right-over r)))
	  (delete-overlay left-over) (delete-overlay right-over)))))

(defun my-evil-tex-toggle-delim-type ()
  "Cycle through (), [] and {}, while keeping \big, \Big, etc."
  (interactive)
  (let* ((case-fold-search nil)
	   (outer (evil-tex-a-delim)) (inner (evil-tex-inner-delim))
	   (left-over (ignore-errors (make-overlay (car outer) (car inner))))
	   (right-over (ignore-errors (make-overlay (cadr inner) (cadr outer)))))
    (when (and left-over right-over outer inner)
	(save-excursion
	  (let ((left-str (buffer-substring-no-properties (overlay-start left-over) (overlay-end left-over)))
		(right-str (buffer-substring-no-properties (overlay-start right-over) (overlay-end right-over))))
	    (cl-destructuring-bind (l . r)
		(cond
		 ((string-match "(" left-str)
		  (cons (replace-regexp-in-string "(" "[" left-str)
			(replace-regexp-in-string ")" "]" right-str)))
		 ((string-match "\\[" left-str)
		  (cons (replace-regexp-in-string "\\[" "\\\\{" left-str)
			(replace-regexp-in-string "\\]" "\\\\}" right-str)))
		 ((string-match "\\\\{" left-str)
		  (cons (replace-regexp-in-string "\\\\{" "(" left-str)
			(replace-regexp-in-string "\\\\}" ")" right-str)))
		 (t (cons left-str right-str))) ; do nothing
	      (evil-tex--overlay-replace left-over  l)
	      (evil-tex--overlay-replace right-over r)))
	  (delete-overlay left-over) (delete-overlay right-over)))))

(defun my-preview-latex-config ()
  "ä¸åœ¨Emacs å¯åŠ¨å'å…ˆ previewï¼Œå†æ”¹å˜é‡',å°±ä¸ä¼šå¤±æ•ˆ."
  (setq preview-default-option-list
        '("displaymath" "floats" "graphics" "textmath" "sections" "footnotes") ; æ‰§è¡Œé¢„è§ˆçš„ç¯å¢ƒ
        preview-preserve-counters t ; ä¿ç•™æ•°å­¦å…¬å¼ç¼–å·
        preview-locating-previews-message nil
        preview-protect-point t     ; é˜²æ­¢å…‰æ ‡(point)è·³è¿›é¢„è§ˆå›¾åƒå†…éƒ¨
        preview-leave-open-previews-visible t ; é˜²æ­¢é¢„è§ˆä¼šé¢‘ç¹é—ªçƒæ¶ˆå¤±
        preview-LaTeX-command-replacements '(preview-LaTeX-disable-pdfoutput)
        preview-auto-interval 0.15

        ;; Uncomment the following only if you have followed the above
        ;; instructions concerning, e.g., hyperref:

        ;; (preview-LaTeX-command-replacements
        ;;  '(preview-LaTeX-disable-pdfoutput))
        preview-pdf-color-adjust-method 'compatible)) ; é¢„è§ˆå›¾ç‰‡ä½¿ç”¨Emacsä¸»é¢˜èƒŒæ™¯è‰²

(defun my-preview-latex-auto-config ()
  ;; å¯ç”¨ tikzpicture ç¯å¢ƒçš„æ”¯æŒ
  ;; éœ€è¦åœ¨æ–‡æ¡£å¯¼è¨€åŒºåŠ å…¥
  ;; \usepackage[displaymath,sections,graphics,floats,textmath]{preview}
  ;; \PreviewEnvironment[{[]}]{tikzpicture}
  ;; å¦‚æœå¯¼è¨€åŒºæ²¡æœ‰è®¾ç½®, ä¼šè¿”å›é”™è¯¯. é»˜è®¤å…³é—­
  ;; (add-to-list 'preview-auto-extra-environments "tikzpicture")
  (add-to-list 'TeX-file-extensions "tex\\.~[^~]+~")
  ;; (preview-auto-setup)   ;; å¦‚æœä¸ä½¿ç”¨ `C-c C-p C-a' é‚£ä¹ˆå¯ä»¥æ³¨é‡Š.
  (preview-auto-conditionally-enable))

;; }}

(defun my-latex-tab-dispatch ()
  "Smart TAB for LaTeX: yasnippet > cdlatex > indent."
  (interactive)
  (cond
   ;; 1. å¦‚æœæ­£åœ¨ snippet field ä¸­ï¼Œä¼˜å…ˆè·³è½¬
   ((and (bound-and-true-p yas-minor-mode)
         (yas--snippets-at-point))
    (yas-next-field-or-maybe-expand))

   ;; 2. å°è¯•å±•å¼€ yasnippetï¼ˆä¸è¦é¢„åˆ¤ï¼‰
   ((and (bound-and-true-p yas-minor-mode)
         (yas-expand))
    t)

   ;; 3. CDLaTeX
   ((bound-and-true-p cdlatex-mode)
    (cdlatex-tab)
    t)

   ;; 4. fallback
   (t
    (indent-for-tab-command))))

;;; Phase 1 â”€â”€ AUCTeX core (tex.el)
;;; Safe: variables read once, never rebuilt automatically
(with-eval-after-load 'tex
  ;; Core AUCTeX behavior
  (my-auctex-setup)
  (my-synctex-setup)
  ;; å¿…é¡»åœ¨auctexåé¢
  (my-auctex-cont-latexmk-setup)

  ;; (outline-minor-mode) ; å¤§çº²é¢„è§ˆ
  ;; (outline-hide-body) ; å¯åŠ¨æ—¶æŠ˜å æ–‡ä»¶

  ;; prettify symbols(å…¨å±€è¡¨ï¼Œåªåšä¸€æ¬¡)
  (my-more-prettified-symbols) ;; LaTeX å‘½ä»¤ä¸ Unicode ç¬¦å·çš„æ˜ å°„, è¾“å…¥'\+ç¬¦å·'

  ;; åœ¨ç¼–è¯‘å®Œæˆååˆ·æ–° pdf æ–‡ä»¶
  (add-hook 'TeX-after-compilation-finished-functions
            #'TeX-revert-document-buffer)

  ;; åœ¨ç¼–è¯‘å®Œæˆååˆ·æ–°æ ‡ç­¾å·å’ŒæŠ˜å 
  (add-hook 'TeX-after-compilation-finished-functions
            #'my-refresh-auctex-label-numbers))

;;; Phase 2 â”€â”€ LaTeX layer (latex.el / font-latex.el)
;;; Safe: initialized once when LaTeX-mode is defined
;; ç¡®ä¿ AUCTeX å·²ç»åŠ è½½
(with-eval-after-load 'latex
  ;; åŠ è½½ CDLaTeX
  (my-ensure 'cdlatex)

  (my-LaTeX-fonts-config)             ;;  LaTeX å­—ä½“æ˜ å°„, å­—ä½“ç¾åŒ–, æ’å…¥å¿«æ·é”®C-c C-f + key
                                      ;;  ç”¨ C-x C-e + key æ”¹å˜é€‰ä¸­æ–‡å­—çš„å­—ä½“.

  ;; cdlatex
  (setq cdlatex-paired-parens "$([{") ;; é»˜è®¤æ¨èæ—  "()" è¡¥å…¨.
  (my-set-cdlatex-command-alist)      ;; å¿«æ·æ’å…¥çš„ LaTeX å®å’Œç¬¦å·, key+<TAB>
  (my-set-cdlatex-env-alist)          ;; å®šä¹‰å¿«æ·æ’å…¥çš„ LaTeX ç¯å¢ƒ, C-c { <env-name> evil: leader+c+name
  (my-set-cdlatex-math-modify-alist)  ;; å®šä¹‰æ•°å­¦æ¨¡å¼ä¸‹çš„å­—ä½“ä¿®é¥°
  (my-set-cdlatex-math-symbol-alist)  ;; å®šä¹‰æ•°å­¦ç¬¦å· ` + key


  (my-reftex-setup)

  ;; preview å˜é‡(åªåˆå§‹åŒ–ä¸€æ¬¡)
  (when (display-graphic-p)
    (my-preview-latex-config))

  ;; after evil
  (my-ensure 'evil-tex)

  ;; ä½¿ç”¨ mt + '+/-/t'
  (define-key evil-tex-toggle-map (kbd "+") #'my-evil-tex-incre-delim-size)
  (define-key evil-tex-toggle-map (kbd "-") #'my-evil-tex-decre-delim-size)
  (define-key evil-tex-toggle-map (kbd "t") #'my-evil-tex-toggle-delim-type)

  (define-key LaTeX-mode-map (kbd "C-c m +") #'my-evil-tex-incre-delim-size)
  (define-key LaTeX-mode-map (kbd "C-c m -") #'my-evil-tex-decre-delim-size)
  (define-key LaTeX-mode-map (kbd "C-c m t") #'my-evil-tex-toggle-delim-type)

  (evil-define-key 'insert LaTeX-mode-map (kbd "TAB") #'my-latex-tab-dispatch))


;; æ‰©å±• AUCTeX çš„ font-latex å¼•ç”¨å®è¯†åˆ«
(with-eval-after-load 'font-latex
  ;; å…ˆè·å–åŸå§‹å¼•ç”¨å®åˆ—è¡¨
  (let ((ref-keywords font-latex-match-reference-keywords))
    ;; æ·»åŠ  cleveref å®
    (setq font-latex-match-reference-keywords
          (append
           '(("cref" "[{")
             ("Cref" "[{")
             ("cpageref" "[{")
             ("Cpageref" "[{"))
           ref-keywords))))

(defun my-latex-hook-function ()
  (turn-on-reftex)
  (when (fboundp 'amsreftex-turn-on)
    (amsreftex-turn-on))
  (my-preview-setup)
  (turn-on-cdlatex)
  (evil-tex-mode)

  ;; folding + label numbers(ä¾èµ– aux)
  (TeX-fold-mode 1)  ;; å¯ç”¨æŠ˜å åŠŸèƒ½, æ¯æ¬¡å¯ç”¨éƒ½ä¼šé‡å»ºçŠ¶æ€, å½±å“my-TeX-fold-config
  (my-label-numbers)

  (turn-on-auto-fill)    ;; è‡ªåŠ¨æ¢è¡Œ, è‡ªåŠ¨è¿›è¡Œç¼©è¿›
  ;; (flymake-mode)    ;; auctexæŸ¥æ‰¾é£æ ¼é”™è¯¯, å¦‚æœä½¿ç”¨æŒç»­ç¼–è¯‘æ›´æ–°æ ‡ç­¾å·éœ€è¦ç¦æ­¢,
  ;; å¦åˆ™å¯èƒ½ä¼šå’Œauctex-cont-latexmkçš„flymake-modeäº§ç”Ÿå†²çª

  (when (display-graphic-p)
    (my-ensure 'preview-auto)
    (my-preview-latex-auto-config)))

;; LaTeXï¼ˆæ— è®ºæ˜¯ AUCTeX æˆ–å†…ç½®ï¼‰å…±åŒ hook
(add-hook 'LaTeX-mode-hook #'my-latex-hook-function t)
;; (add-hook 'latex-mode-hook #'my-latex-hook-function t)

;; å¿…é¡»æ”¾åœ¨ TeX-fold-mode-hook æˆ–å…¶åæ‰§è¡Œ, å¦åˆ™éšæ—¶ä¼šè¢«é‡ç½®
;; æŠ˜å  LaTeX æ–‡æ¡£ä¸­çš„ç¯å¢ƒ, å®å’Œæ³¨é‡Š
;; ä»»ä½• TeX-fold-* çš„ä¿®æ”¹ï¼Œåªèƒ½æ”¾è¿™é‡Œ
(add-hook 'TeX-fold-mode-hook #'my-TeX-fold-config)

;; (with-eval-after-load 'tex-fold
;;   (add-hook 'TeX-fold-mode-hook
;;             #'my-TeX-fold-config))


(provide 'init-LaTeX-mode)
;;; init-LaTeX-mode.el ends here
